GENES = ["CMY",\
			"CTX-M",\
			"GES",\
			"IMP",\
			"KPC",\
			"NDM",\
			"PER",\
			"SHV",\
			"TEM",\
			"VEB",\
			"VIM"]

FOCAL_GENES = ["CMY-2","CTX-M-15","CTX-M-65","GES-24","IMP-1","KPC-2","NDM-1","PER-1","SHV-134","TEM-1","VIM-1"]

rule all:
	input:
		expand("plots/NCBI-variants/{gene}.pdf", gene=GENES),
		expand("dists/{gene}.tsv", gene=GENES),
		expand("assignments/{focal_gene}.csv", focal_gene=FOCAL_GENES)	


rule extract_NCBI_gene:
	input:
		"data/NCBI_bl.fa"
	params:
		gene_name="{gene}"
	output:
		"fasta/{gene}.fa"
	script:
		"scripts/extract_gene.py"

rule align_NCBI_gene:
	input:
		"fasta/{gene}.fa"
	output:
		"alns/{gene}.aln"
	shell:
		"mafft --auto {input} > {output}"

rule snp_dists_NCBI:
	input:
		"alns/{gene}.aln"
	output:
		"dists/{gene}.tsv"
	shell:
		"snp-dists -a -b {input} > {output}"

rule run_fasttree_NCBI:
	input:
		"alns/{gene}.aln"
	output:
		"trees/{gene}.nwk"
	shell:
		"FastTree -nt -gtr {input} > {output}"

rule refine_fasttree_NCBI:
	input:
		tree= "trees/{gene}.nwk",
		aln= "alns/{gene}.aln"
	output:
		"refined-trees/{gene}.nwk"
	shell:
		"python scripts/refine_tree.py --tree_in {input.tree} --aln {input.aln} --tree_out {output}"

rule plot_tree_NCBI:
	input:
		"refined-trees/{gene}.nwk"
	output:
		"plots/NCBI-variants/{gene}.pdf"
	script:
		"scripts/plot_tree.R"

rule run_metadata:
	input:
		metadata="data/metadata.csv"
	script:
		"scripts/metadata_gene_combinations.py"

rule extract_focal_genes:
	input:
		"data/NCBI_bl.fa"
	params:
		gene_name="{focal_gene}"
	output:
		"focal_genes/{focal_gene}.fa"
	script:
		"scripts/extract_focal_genes_from_ncbi.py"


rule extract_genes_from_contigs:
	input:
		"focal_genes/{focal_gene}.fa"
	output:
		"sequences/{focal_gene}.fa"
	shell:
		"python scripts/extract_region_around_gene.py --gene {input} --input_fasta test.fa --output_fasta {output} --upstream 0 --downstream 0"

rule name_observed_genes:
	input:
		fasta="sequences/{focal_gene}.fa",
		variants="fasta/{focal_gene}.fa"
	output:
		"assignments/{focal_gene}.csv"
	shell:
		"python scripts/name_variants.py --variant_fasta {input.variants} --output_file {output} --input_fasta {input.fasta}"



