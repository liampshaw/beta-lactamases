GENES = ["CMY",\
			"CTX-M",\
			"GES",\
			"IMP",\
			"KPC",\
			"NDM",\
			"PER",\
			"SHV",\
			"TEM",\
			"VEB",\
			"VIM"]

rule all:
	input:
		expand("plots/{gene}.pdf", gene=GENES)

rule extract_gene:
	input:
		"data/NCBI_bl.fa"
	params:
		gene_name="{gene}"
	output:
		"fasta/{gene}.fa"
	script:
		"scripts/extract_gene.py"


rule align_gene:
	input:
		"fasta/{gene}.fa"
	output:
		"alns/{gene}.aln"
	shell:
		"mafft --auto {input} > {output}"


rule run_fasttree:
	input:
		"alns/{gene}.aln"
	output:
		"trees/{gene}.nwk"
	shell:
		"FastTree -nt -gtr {input} > {output}"

rule refine_tree:
	input:
		tree= "trees/{gene}.nwk",
		aln= "alns/{gene}.aln"
	output:
		"refined-trees/{gene}.nwk"
	shell:
		"python scripts/refine_tree.py --tree_in {input.tree} --aln {input.aln} --tree_out {output}"

rule plot_tree:
	input:
		"refined-trees/{gene}.nwk"
	output:
		"plots/{gene}.pdf"
	script:
		"scripts/plot_tree.R"

